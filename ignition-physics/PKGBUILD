# Maintainer: acxz <akashpatel2008 at yahoo dot com>

pkgname=ignition-physics
pkgver=7.0.0
pkgrel=1
pkgdesc="Abstract physics interface designed to support simulation and rapid
development of robot applications."
arch=('x86_64')
url="https://gazebosim.org/libs/physics"
license=('Apache')
depends=('ignition-cmake>2' 'ignition-math>6' 'ignition-plugin'
         'ignition-common>3' 'libdart' 'sdformat>9')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gazebosim/gz-physics/archive/gz-physics7_${pkgver}.tar.gz"
        "645.patch::https://github.com/gazebosim/gz-physics/pull/645.patch")
sha256sums=('d352b68bec5d2e28cb20237d2cb68917e4acfe122e6df0dc059ed436d65fd270'
            '5c80c53c61fb8080123fd8a05c5652a138ca67b76d95ae5f2fd32f5b954fe32f')

_dir="gz-physics-gz-physics7_${pkgver}"

prepare() {
  cd "$srcdir/$_dir"
  patch -Np1 --verbose -i ../645.patch
}

build() {
  cd "$srcdir/$_dir"

  mkdir -p build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE="Release" \
           -DCMAKE_INSTALL_PREFIX="/usr" \
           -DCMAKE_INSTALL_LIBDIR="lib" \
           -DBUILD_TESTING=OFF

  make
}

package() {
  cd "$srcdir/$_dir/build"
  make DESTDIR="${pkgdir}/" install
}
